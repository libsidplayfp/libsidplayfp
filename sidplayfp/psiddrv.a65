; 
; This file is part of libsidplayfp, a SID player engine.
; 
; Copyright 2014 Leandro Nini <drfiemost@users.sourceforge.net>
; Copyright 2001-2004 Simon White
; Copyright 2000 Dag Lem
; 
; This program is free software; you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation; either version 2 of the License, or
; (at your option) any later version.
; 
; This program is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
; 
; You should have received a copy of the GNU General Public License
; along with this program; if not, write to the Free Software
; Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301, USA.
; 


;**************************************************************************
; psiddrv.a65  -  C64 Sid Player
; 
; written by Dag Lem
; modified by Simon White for use in the sidplayer SIDPlay2.
; 
; Build with:
; xa -R -G psiddrv.a65 -o psiddrv.o65
; od -v -An -w8 -tx1 psiddrv.o65 | sed -re 's/[[:alnum:]]+/0x&,/g' > psiddrv.bin
;**************************************************************************/

            ; entry address
coldvec     .word cold

            ; initial user interrupt vectors
irqusr      .word irqjob
brkusr      .word irqret
nmiusr      .word nmijob

            ; redirect basic restart vector
            ; to finish the init sequence
            ; (hooks in via stop function)
stopusr     .word setiomap

playnum     .byte 0
speed       .byte 0
initvec     .word 0
playvec     .word 0
rndwait     .word 0
initiomap   .byte 0
playiomap   .byte 0
video       .byte 0
clock       .byte 0
flags       .byte 0

            ; init/play PSID
play        jmp (playvec)
init        jmp (initvec)

            ; cold start
cold        sei

            ; Turn interrupts off and
            ; clear any pending irqs
            lda #$00
            sta $d01a
            lda $d019
            sta $d019
            lda #$7f
            sta $dc0d
            sta $dd0d
            lda $dc0d
            lda $dd0d

doinit      ; setup hardware

            ; Set CIA 1 Timer A to 50/60Hz
            lda video
            beq ntsc
pal         lda #$25
            ldx #$40
            jmp timer
ntsc        lda #$95
            ldx #$42
timer       sta $dc04
            stx $dc05

            ; Maximum volume
            lda #$0f
            sta $d418

            ; set VICII raster to line 311 for RSIDs
            ldx #$9b
            ldy #$37

            ; we should use the proper values for
            ; the default raster, however if the tune
            ; is playing at the wrong speed (e.g.
            ; PAL at NTSC) use the compatibility
            ; raster instead to try make it work
            lda video
            eor clock
            ora initiomap
            beq vicinit

            ; set VICII raster to line 0 for PSIDs
            ; (compatibility raster)
            ldx #$1b
            ldy #$00
vicinit     stx $d011
            sty $d012

            ; Don't override default irq handler for RSIDs
            lda initiomap
            beq random

            ; If play address, override default irq vector so
            ; we reach our routine to handle play routine
            lda playiomap
            beq random
            ldx #<irqjob
            stx $0314

            ; simulate time before user loads tune
random      ldx rndwait
            ldy rndwait+1
            inx
            iny
wait        dex
            bne wait
            dey
            bne wait

            ; 0 indicates VIC timing (PSIDs only)
            ; else it's from CIA
            lda speed
            bne ciainit

            ; enable VICII raster interrupt
            lda #$81
            sta $d01a
            jmp setiomap

            ; enable CIA 1 timer A interrupt
ciainit     lda #$81
            ldx #$01
            sta $dc0d
            stx $dc0e

setiomap    ; set I/O map and call song init routine
            lda initiomap
            bne setbank

            ; Only release interrupt mask for real
            ; C64 tunes (initiomap = 0) thus
            ; providing a more realistic environment
            lda #$37
setbank     sta $01

setregs     lda flags
            pha
            lda playnum
            plp
            jsr init
            lda initiomap
            beq idle
            lda playiomap
            beq run
            lda #$37
            sta $01

run         cli
idle        jmp idle

irqjob      lda $01
            pha
            lda playiomap
            sta $01
            lda #0
            jsr play
            pla
            sta $01
            dec $d019
            lda $dc0d
irqret      pla
            tay
            pla
            tax
            pla
            rti

nmijob      bit $dd0d
            rti

.end
